# Find LLVM
#
# It defines the following variables
# LLVM_FOUND - True if llvm found.
# LLVM_INCLUDE_DIRS - where to find llvm include files
# LLVM_LIBRARY_DIRS - where to find llvm libs
# LLVM_LIBRARIES - where to find llvm libs

INCLUDE (FindPackageHandleStandardArgs)

SET(llvm_config_names llvm-config
  llvm-config-3.5 llvm-config-mp-3.5
  llvm-config-3.4 llvm-config-mp-3.4
  llvm-config-3.3 llvm-config-mp-3.3
  llvm-config-3.2 llvm-config-mp-3.2
  llvm-config-3.1 llvm-config-mp-3.1)
FIND_PROGRAM(LLVM_CONFIG_EXECUTABLE NAMES ${llvm_config_names})

if (LLVM_CONFIG_EXECUTABLE)
  MESSAGE(STATUS "LLVM llvm-config found at: ${LLVM_CONFIG_EXECUTABLE}")
else (LLVM_CONFIG_EXECUTABLE)
  MESSAGE(FATAL_ERROR "Could NOT find LLVM executable")
endif (LLVM_CONFIG_EXECUTABLE)

EXECUTE_PROCESS(
  COMMAND ${LLVM_CONFIG_EXECUTABLE} --prefix
  OUTPUT_VARIABLE LLVM_ROOT
  OUTPUT_STRIP_TRAILING_WHITESPACE
)

EXECUTE_PROCESS(
  COMMAND ${LLVM_CONFIG_EXECUTABLE} --includedir
  OUTPUT_VARIABLE LLVM_INCLUDE_DIR
  OUTPUT_STRIP_TRAILING_WHITESPACE
)

EXECUTE_PROCESS(
  COMMAND ${LLVM_CONFIG_EXECUTABLE} --libdir
  OUTPUT_VARIABLE LLVM_LIBRARY_DIR
  OUTPUT_STRIP_TRAILING_WHITESPACE
)

SET (LLVM_INCLUDE_DIRS ${LLVM_INCLUDE_DIR})
SET (LLVM_LIBRARY_DIRS ${LLVM_LIBRARY_DIR})

# Find the library as well, some systems may require to link it.
FIND_LIBRARY(LLVM_LIBRARY NAMES llvm
  HINTS
  ${LLVM_ROOT}/lib
  ${LLVM_LIBRARY_DIRS}
  DOC "libllvm")

SET (LLVM_LIBRARIES ${LLVM_LIBRARY})

MARK_AS_ADVANCED (LLVM_ROOT LLVM_INCLUDE_DIRS LLVM_LIBRARY_DIRS LLVM_LIBRARY)

FIND_PACKAGE_HANDLE_STANDARD_ARGS (LLVM REQUIRED_VARS LLVM_ROOT
  LLVM_INCLUDE_DIRS LLVM_LIBRARY_DIRS LLVM_LIBRARY)
